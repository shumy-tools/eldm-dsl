/*
 * generated by Xtext 2.15.0
 */
package net.eldm.validation

import com.google.inject.Inject
import net.eldm.eldmDsl.EldmDslPackage
import net.eldm.eldmDsl.EnumDef
import net.eldm.eldmDsl.LetValue
import net.eldm.eldmDsl.MapDef
import net.eldm.eldmDsl.MapEntryDef
import net.eldm.eldmDsl.TypeDef
import net.eldm.util.TypeResolver
import net.eldm.util.TypeValidator
import org.eclipse.emf.ecore.EObject
import org.eclipse.xtext.validation.Check

/**
 * This class contains custom validation rules. 
 *
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#validation
 */
class EldmDslValidator extends AbstractEldmDslValidator {
  @Inject extension TypeValidator tValidator
  @Inject extension TypeResolver tResolver
  
  def error(EObject source, String message) {
    error(message, source.eContainer, source.eContainingFeature)
  }
  
  /*TODO: required validations
   * Verify already existing names for definitions
   * Verify the use of reserved keywords in map and enum entries
  */
  
  @Check
  def void checkLetCase(LetValue it) {
    if (name != name.toLowerCase)
      warning("Incorrect name for let-value! Set all chars to lower-case.", it, EldmDslPackage.Literals.LET_VALUE__NAME)
  }
  
  @Check
  def checkTypeDefCase(TypeDef it) {
    if (parser !== null) {
      if (name != name.toLowerCase)
        warning("Incorrect name for string-type! Set all chars to lower-case.", it, EldmDslPackage.Literals.DEFINITION__NAME)
    } else
      if (name != name.toFirstUpper)
        warning("Incorrect name for structure! Set the first char to upper-case.", it, EldmDslPackage.Literals.DEFINITION__NAME)
  }
  
  @Check
  def void checkKeyDefCase(MapEntryDef it) {
    if (name != name.toLowerCase)
      warning("Incorrect name for key! Set all chars to lower-case.", it, EldmDslPackage.Literals.MAP_ENTRY_DEF__NAME)
  }
  
  @Check
  def void checkMapDefUniqueKeys(MapEntryDef kd) {
    val parent = (kd.eContainer as MapDef)
    if (parent.defs.filter[name == kd.name].size > 1)
      error("Multiple keys with the same name.", kd, EldmDslPackage.Literals.MAP_ENTRY_DEF__NAME)
  }
  
  @Check
  def void checkEnumDef(EnumDef ed) {
    if (ed.type === null) {
      ed.defs.forEach[
        if (value !== null)
          error("Enum has no value definition.", it, EldmDslPackage.Literals.ENUM_ITEM_DEF__VALUE)
      ]
      return
    }
      
    ed.defs.forEach[ value.is(ed.type) ]
  }
  
  @Check
  def void checkLetValue(LetValue lv) {
    val resDef = lv.result.inferType
    lv.is(resDef)
  }
}
